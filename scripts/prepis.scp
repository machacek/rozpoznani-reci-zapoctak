# Hrubý fonetický přepis (skript programu sed, používán ve spojení s init.scp)
# 11.9.1997 Autor: Nino Peterek, peterek@ufal.ms.mff.cuni.cz

# namapování nechtěných znaků na model ticha
s/^.*[0-9].*$/sil/g

# náhrada víceznakových fonémů speciálním znakem, případně rozepsání znaku na více fonémů
s/CH/#/g
s/W/V/g
s/Q/KV/g
s/DŽ/&/g
s/DZ/@/g
s/X/KS/g

# ošetření Ě 
s/\([BPFV]\)Ě/\1JE/g
s/DĚ/ĎE/g
s/TĚ/ŤE/g
s/NĚ/ŇE/g
s/MĚ/MŇE/g

# změkčující i
s/DI/ĎI/g
s/TI/ŤI/g
s/NI/ŇI/g
s/DÍ/ĎÍ/g
s/TÍ/ŤÍ/g
s/NÍ/ŇÍ/g

# asimilace znělosti
s/B$/P/g
s/B\([PTŤKSŠCČ#F]\)/P\1/g
s/B\([BDĎGZŽ@&H]\)$/P\1/g
s/P\([BDĎGZŽ@&H]\)/B\1/g
s/D$/T/g
s/D\([PTŤKSŠCČ#F]\)/T\1/g
s/D\([BDĎGZŽ@&H]\)$/T\1/g
s/T\([BDĎGZŽ@&H]\)/D\1/g
s/Ď$/Ť/g
s/Ď\([PTŤKSŠCČ#F]\)/Ť\1/g
s/Ď\([BDĎGZŽ@&H]\)$/Ť\1/g
s/Ť\([BDĎGZŽ@&H]\)/Ď\1/g
s/V$/F/g
s/V\([PTŤKSŠCČ#F]\)/F\1/g
s/V\([BDĎGZŽ@&H]\)$/F\1/g
s/F\([BDĎGZŽ@&H]\)/V\1/g
s/G$/K/g
s/G\([PTŤKSŠCČ#F]\)/K\1/g
s/G\([BDĎGZŽ@&H]\)$/K\1/g
s/K\([BDĎGZŽ@&H]\)/G\1/g
s/Z$/S/g
s/Z\([PTŤKSŠCČ#F]\)/S\1/g
s/Z\([BDĎGZŽ@&H]\)$/S\1/g
s/S\([BDĎGZŽ@&H]\)/Z\1/g
s/Ž$/Š/g
s/Ž\([PTŤKSŠCČ#F]\)/Š\1/g
s/Ž\([BDĎGZŽ@&H]\)$/Š\1/g
s/Š\([BDĎGZŽ@&H]\)/Ž\1/g
s/H$/#/g
s/H\([PTŤKSŠCČ#F]\)/#\1/g
s/H\([BDĎGZŽ@&H]\)$/#\1/g
s/#\([BDĎGZŽ@&H]\)/H\1/g
s/@$/C/g
s/@\([PTŤKSŠCČ#F]\)/C\1/g
s/@\([BDĎGZŽ@&H]\)$/C\1/g
s/C\([BDĎGZŽ@&H]\)/@\1/g
s/&$/Č/g
s/&\([PTŤKSŠCČ#F]\)/Č\1/g
s/&\([BDĎGZŽ@&H]\)$/Č\1/g
s/Č\([BDĎGZŽ@&H]\)/&\1/g
s/Ř$/>/g
s/Ř\([PTŤKSŠCČ#F]\)/>\1/g
s/Ř\([BDĎGZŽ@&H]\)$/>\1/g
s/\([PTŤKSŠCČ#F]\)Ř/\1>/g


#zbytek
s/NK/ng K/g
s/NG/ng G/g
s/MV/mg V/g
s/MF/mg F/g
s/NŤ/ŇŤ/g
s/NĎ/ŇĎ/g
s/NŇ/Ň/g
s/CC/C/g
s/DD/D/g
s/JJ/J/g
s/KK/K/g
s/LL/L/g
s/NN/N/g
s/MM/M/g
s/SS/S/g
s/TT/T/g
s/ZZ/Z/g
s/ČČ/Č/g
s/ŠŠ/Š/g
s/-//g

# závěrečný přepis na HTK abecedu
s/>/rsz /g
s/EU/eu /g
s/AU/au /g
s/OU/ou /g
s/Á/aa /g
s/Č/cz /g
s/Ď/dj /g
s/É/ee /g
s/Í/ii /g
s/Ň/nj /g
s/Ó/oo /g
s/Ř/rzs /g
s/Š/sz /g
s/Ť/tj /g
s/Ú/uu /g
s/Ů/uu /g
s/Ý/ii /g
s/Ž/zs /g
s/Y/i /g
s/&/dzs /g
s/@/ts /g
s/#/ch /g
s/!//g
s/\([A-Z]\)/\1 /g
s/$/ sp/g
